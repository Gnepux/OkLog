buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'si.kamino.soter'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.github.simonpercic.oklogexample"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    signingConfigs {
        release.initWith(signingConfigs.debug)
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            useProguard true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        textReport true
        warningsAsErrors true
        lintConfig file('../config/check/lint/lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        oklog {
            applicationIdSuffix ".oklog"
        }

        oklog3 {
            applicationIdSuffix ".oklog3"
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

ext.daggerVersion = '2.10'
ext.butterknifeVersion = '8.6.0'
ext.retrofit2Version = '2.3.0'
ext.okHttp3Version = '3.8.0'

dependencies {
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.timber:timber:4.5.1'

    compile "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    oklog3Compile project(':oklog3')
    oklog3Compile "com.squareup.retrofit2:retrofit:$retrofit2Version"
    oklog3Compile "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    oklog3Compile "com.squareup.retrofit2:adapter-rxjava:$retrofit2Version"
    oklog3Compile "com.squareup.okhttp3:okhttp:$okHttp3Version"
    oklog3Compile "com.squareup.okhttp3:logging-interceptor:$okHttp3Version"

    oklogCompile project(':oklog')
    oklogCompile 'com.squareup.retrofit:retrofit:1.9.0'
    oklogCompile 'com.squareup.okhttp:okhttp:2.7.5'

    // Static code rules
    checkstyleRules files('../config/soter/checkstyle-0.3.xml')
    findbugsRules files('../config/soter/findbugs-0.1.xml')
    pmdRules files('../config/soter/pmd-0.1.xml')
}

soter {
    checkstyle {
        enabled true
    }

    findbugs {
        enabled true
        reportLevel 'high'
    }

    pmd {
        enabled true
    }
}
