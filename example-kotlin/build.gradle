buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
mainClassName = 'com.github.simonpercic.oklogexamplekotlin.MainKt'

ext.daggerVersion = '2.10'
ext.retrofit2Version = '2.3.0'
ext.okHttp3Version = '3.8.0'

kapt {
    generateStubs = true
}

configurations {
    ktlint
}

dependencies {
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // OkLog3
    compile project(':oklog3')
    compile "com.squareup.retrofit2:retrofit:$retrofit2Version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    compile "com.squareup.okhttp3:okhttp:$okHttp3Version"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttp3Version"

    // OkLog
    compile project(':oklog')
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.7.5'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    ktlint "com.github.shyiko:ktlint:$ktlintVersion"
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}
